GRAMATICA   (se realizaron cambios asi que no es la exactamente la misma que se utilizo en el analizador)

<INIT>::=       /   <START>

<START>::=      /   main :  <LIST_INSTR> <LIST_LABEL>
                |   main :  <LIST_INSTR>

<LIST_INSTR>::= /   <LIST_INSTR> <INSTR>
                |   <INSTR>

<INSTR>::=      /   <ASSIGNATION>
                |   <GOTO>
                |   <PRINT>
                |   <EXIT>
                |   <UNSET>
                |   <IF>

<LIST_LABEL>::= /   <LIST_LABEL> <LABEL>
                |   <LABEL>

<LABEL>::=      /   id : <LIST_INSTR>

<ASSIGNATION>::=/   <REGISTER> = <EXPRESSION> ;

<REGISTER>::=   /   <S_REGISTER>
                |   <ARRAY_REGISTER>

<S_REGISTER>::= /   temp
                |   param
                |   returned_value
                |   returned_value_lvl
                |   stack_value
                |   stack_pointer

<ARRAY_REGISTER>::=/<REGISTER> <LIST_ARRAY>

<LIST_ARRAY>::= /   <LIST_ARRAY> [ <ARRAY_CONT> ]
                |   [ <ARRAY_CONT> ]

<ARRAY_CONT>::= /   <PRIMITIVE>
                |   <S_REGISTER>

<EXPRESSION>::= /   <SIMPLE_UNARY>
                |   <CONVERSION>
                |   <ARITHMETIC>
                |   <LOGIC>
                |   <BIT>
                |   <RELATIONAL>

<SIMPLE_UNARY>::=/  <PRIMITIVE>
                |   <REGISTER>
                |   - <REGISTER>
                |   & <REGISTER>    //OPB_AND
                |   read ( )
                |   array ( )

<PRIMITIVE>::=  /   number
                |   decimal
                |   string

<CONVERSION>::= /   ( int ) <REGISTER>
                |   ( float ) <REGISTER>
                |   ( char ) <REGISTER>

<ARITHMETIC>::= /   <REGISTER> + <REGISTER>
                |   <REGISTER> - <REGISTER>
                |   <REGISTER> * <REGISTER>
                |   <REGISTER> / <REGISTER>
                |   <REGISTER> % <REGISTER>
                |   abs ( <REGISTER> )

<LOGIC>::=      /   ! <REGISTER>
                |   <REGISTER> && <REGISTER>
                |   <REGISTER> || <REGISTER>
                |   <REGISTER> xor <REGISTER>

<BIT>::=        /   ~ <REGISTER>
                |   <REGISTER> & <REGISTER>
                |   <REGISTER> | <REGISTER>
                |   <REGISTER> ^ <REGISTER>
                |   <REGISTER> << <REGISTER>
                |   <REGISTER> >> <REGISTER>

<RELATIONAL>::= /   <REGISTER> == <REGISTER>
                |   <REGISTER> != <REGISTER>
                |   <REGISTER> >= <REGISTER>
                |   <REGISTER> <= <REGISTER>
                |   <REGISTER> > <REGISTER>
                |   <REGISTER> < <REGISTER>

<GOTO>::=       /   goto id ;

<PRINT>::=      /   print ( <EXPRESSION> )

<EXIT>::=       /   exit ;

<UNSET>::=      /   unset ( <REGISTER> ) ;

<IF>::=         /   if ( <EXPRESSION> ) <GOTO>